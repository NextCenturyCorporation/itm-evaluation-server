buildscript {
  repositories {
    gradlePluginPortal()
  }
  dependencies {
    classpath("org.openapi.generator:org.openapi.generator.gradle.plugin:7.11.0")
  }
}
apply plugin:'base'
apply plugin: 'org.openapi.generator'

defaultTasks 'clean', 'generate', 'postGeneration'

def generatedApiDirectory = "generated"
def generatedOutputDirectory = "openapi_server"
def apiDirectory = "swagger"
def outputDirectory = "swagger_server"

task swaggerClean(type: Delete) {
    doFirst {
        delete generatedApiDirectory
    }
    mustRunAfter "clean"
}

// Define a task for validating the primary YAML
task validateMainSpec(type: org.openapitools.generator.gradle.plugin.tasks.ValidateTask) {
    inputSpec.set("$projectDir/$apiDirectory/swagger.yaml")
}

// Define a task for validating the domain-specific YAML
task validateDomainSpec(type: org.openapitools.generator.gradle.plugin.tasks.ValidateTask) {
    inputSpec.set("$projectDir/$apiDirectory/domain_swagger.yaml")
}

// Define a task to validate all YAML files
task validate(dependsOn: ['validateMainSpec', 'validateDomainSpec'])

// Define a task to clean then generate
task generate(dependsOn: ['swaggerClean', 'openApiGenerate'])

openApiGenerate {
    generatorName = "python-flask"
    inputSpec = "$projectDir/$apiDirectory/swagger.yaml".toString()
    outputDir = "$projectDir/$generatedApiDirectory".toString()
    globalProperties = [
            modelDocs: "false"
    ]
    skipValidateSpec = true
    logToStderr = true
    generateAliasAsModel = false
    // set to true and set environment variable {LANG}_POST_PROCESS_FILE
    // (e.g. SCALA_POST_PROCESS_FILE) to the linter/formatter to be processed.
    // This command will be passed one file at a time for most supported post processors.
//    enablePostProcessFile = false
}

task postGeneration() {
    mustRunAfter "openApiGenerate"
    doLast() {
        // Copy generated files into models folder
        copy {
            from "${generatedApiDirectory}/${generatedOutputDirectory}/models"
            into "${outputDirectory}/models"
        }

        // delete generated api directory
        // delete generatedApiDirectory
        println "removed ${generatedApiDirectory}"
    }
}

// Ensures tasks are re-run always, needed for openApiGenerate
gradle.taskGraph.whenReady { taskGraph ->
  taskGraph.getAllTasks().each {
      it.setOnlyIf { true }
      it.outputs.upToDateWhen { false }
  }
}